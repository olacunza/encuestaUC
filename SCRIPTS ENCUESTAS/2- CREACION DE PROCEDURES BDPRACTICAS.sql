--SERVIDOR -> SRVHWDBTS24
USE BDPRACTICAS
GO

/*===========================================================
NOMBRE: ENCUESTA.sp_CrearEncuestaAsignatura
FECHA: 18/10/2025
AUTOR: ORESTES LA CUNZA - YACURUNA TECHNOLOGIES
OBJETIVO: CREAR LA ENCUESTA FINAL QUE ES ENVIADA AL CORREO
MODIFICACIONES:
NRO		FECHA		USUARIO		MODIFICACION
001		DD/MM/YYYY	
===========================================================*/
CREATE PROCEDURE ENCUESTA.sp_CrearEncuestaAsignatura
(
@NOMBRE_ENCUESTA VARCHAR(100)
,@DESCRIPCION_ENCUESTA VARCHAR(200)
,@TIPO_ENCUESTA_ID INT
,@TIPO_ENCUESTADO_ID INT
,@TIPO_PROGRAMA VARCHAR(200)
,@SEDE VARCHAR(50)
,@PERIODO_ID VARCHAR(20)
,@SECCION_ID VARCHAR(30)
,@MODULO VARCHAR(500)
,@DOCENTE VARCHAR(250)
,@FECHA_INICIO DATETIME
,@FECHA_FIN DATETIME
,@ACTIVO BIT
,@FECHA_ENVIO DATETIME
,@USUARIO_ENVIO VARCHAR(50)
)
AS
BEGIN
SET NOCOUNT ON;

	DECLARE @ENCUESTA_ID INT;

	INSERT INTO ENCUESTA.tblEncuestaAsignatura (
	NOMBRE_ENCUESTA
	,DESCRIPCION_ENCUESTA
	,TIPO_ENCUESTA
	,TIPO_ENCUESTADO
	,TIPO_PROGRAMA
	,SEDE
	,PERIODO
	,SECCION
	,MODULO
	,DOCENTE
	,FECHA_INICIO
	,FECHA_FIN
	,ACTIVO
	,FECHA_ENVIO
	,USUARIO_ENVIO
	)
	VALUES (
	@NOMBRE_ENCUESTA
	,@DESCRIPCION_ENCUESTA
	,@TIPO_ENCUESTA_ID
	,@TIPO_ENCUESTADO_ID
	,@TIPO_PROGRAMA
	,@SEDE
	,@PERIODO_ID
	,@SECCION_ID
	,@MODULO
	,@DOCENTE
	,@FECHA_INICIO
	,@FECHA_FIN
	,@ACTIVO
	,@FECHA_ENVIO
	,@USUARIO_ENVIO
	)

	SET @ENCUESTA_ID = SCOPE_IDENTITY();
	SELECT @ENCUESTA_ID;

END
GO

/*===========================================================
NOMBRE: ENCUESTA.sp_CrearBloqueAsignatura
FECHA: 18/10/2025
AUTOR: ORESTES LA CUNZA - YACURUNA TECHNOLOGIES
OBJETIVO: CREAR EL BLOQUE DE LA ENCUESTA ASIGNATURA ASOCIADA A LA ENCUESTA FINAL ENVIADA POR CORREO
MODIFICACIONES:
NRO		FECHA		USUARIO		MODIFICACION
001		DD/MM/YYYY	
===========================================================*/
CREATE PROCEDURE ENCUESTA.sp_CrearBloqueAsignatura
(
@ENCUESTA_ID INT
,@TITULO_BLOQUE VARCHAR(200)
,@ORDEN INT
,@FECHA_CREACION DATETIME
,@USUARIO_CREACION VARCHAR(50)
)
AS
BEGIN
SET NOCOUNT ON;

	DECLARE @BLOQUE_ID INT;

	INSERT INTO ENCUESTA.tblEncuestaBloqueAsignatura (
	ENCUESTA_ID
	,TITULO_BLOQUE
	,ORDEN
	,FECHA_CREACION
	,USUARIO_CREACION
	)
	VALUES (
	@ENCUESTA_ID
	,@TITULO_BLOQUE
	,@ORDEN
	,@FECHA_CREACION
	,@USUARIO_CREACION
	)

	SET @BLOQUE_ID = SCOPE_IDENTITY();
	SELECT @BLOQUE_ID;

END
GO

/*===========================================================
NOMBRE: ENCUESTA.sp_CrearPreguntaAsignatura
FECHA: 18/10/2025
AUTOR: ORESTES LA CUNZA - YACURUNA TECHNOLOGIES
OBJETIVO: CREAR LAS PREGUNTAS QUE VAN ASOCIADAS A LOS BLOQUES DE LAS ENCUESTAS FINALES QUE SON ENVIADAS POR CORREO
MODIFICACIONES:
NRO		FECHA		USUARIO		MODIFICACION
001		DD/MM/YYYY	
===========================================================*/
CREATE PROCEDURE ENCUESTA.sp_CrearPreguntaAsignatura
(
@BLOQUE_ID INT
,@TEXTO_PREGUNTA VARCHAR(200)
,@TIPO_PREGUNTA VARCHAR(MAX)
,@ORDEN INT
,@OPCIONES_JSON VARCHAR(MAX)
,@FECHA_CREACION DATETIME
,@USUARIO_CREACION VARCHAR(50)
)
AS
BEGIN
SET NOCOUNT ON;

	INSERT INTO ENCUESTA.tblEncuestaPreguntaAsignatura (
	BLOQUE_ID
	,TEXTO_PREGUNTA
	,TIPO_PREGUNTA
	,ORDEN
	,OPCIONES_JSON
	,FECHA_CREACION
	,USUARIO_CREACION
	)
	VALUES (
	@BLOQUE_ID
	,@TEXTO_PREGUNTA
	,@TIPO_PREGUNTA
	,@ORDEN
	,@OPCIONES_JSON
	,@FECHA_CREACION
	,@USUARIO_CREACION
	);

END
GO

/*===========================================================
NOMBRE: ENCUESTA.sp_CrearEncuestaPlantilla
FECHA: 18/10/2025
AUTOR: ORESTES LA CUNZA - YACURUNA TECHNOLOGIES
OBJETIVO: CREACIÓN DE LA PLANTILLA DE LA ENCUESTA QUE SERVIRÁ PARA ENVIAR LA ENCUESTA FINAL POSTERIORMENTE
MODIFICACIONES:
NRO		FECHA		USUARIO		MODIFICACION
001		DD/MM/YYYY	
===========================================================*/
CREATE PROCEDURE ENCUESTA.sp_CrearEncuestaPlantilla
(
@NOMBRE_ENCUESTA VARCHAR(50)
,@DESCRIPCION_ENCUESTA VARCHAR(200)
,@TIPO_ENCUESTA_ID INT
,@FECHA_CREACION DATETIME
,@USUARIO_CREACION VARCHAR(50)
)
AS
BEGIN
SET NOCOUNT ON;

	DECLARE @ENCUESTA_ID INT;

	INSERT INTO ENCUESTA.tblEncuestaPlantilla (
	NOMBRE_ENCUESTA
	,DESCRIPCION_ENCUESTA
	,TIPO_ENCUESTA
	,ESTADO
	,FECHA_CREACION
	,USUARIO_CREACION
	)
	VALUES (
	@NOMBRE_ENCUESTA
	,@DESCRIPCION_ENCUESTA
	,@TIPO_ENCUESTA_ID
	,1
	,@FECHA_CREACION
	,@USUARIO_CREACION
	)

	SET @ENCUESTA_ID = SCOPE_IDENTITY();
	SELECT @ENCUESTA_ID;

END

GO

/*===========================================================
NOMBRE: ENCUESTA.sp_CrearBloquePlantilla
FECHA: 18/10/2025
AUTOR: ORESTES LA CUNZA - YACURUNA TECHNOLOGIES
OBJETIVO: CREACIÓN DEL BLOQUE QUE VA A ASOCIADO A LA ECUESTA PLANTILLA
MODIFICACIONES:
NRO		FECHA		USUARIO		MODIFICACION
001		DD/MM/YYYY	
===========================================================*/
CREATE PROCEDURE ENCUESTA.sp_CrearBloquePlantilla
(
@ENCUESTA_ID INT
,@TITULO_BLOQUE VARCHAR(200)
,@ORDEN INT
,@ESTADO BIT
,@FECHA_CREACION DATETIME
,@USUARIO_CREACION VARCHAR(50)
)
AS
BEGIN
SET NOCOUNT ON;

	DECLARE @BLOQUE_ID INT;

	INSERT INTO ENCUESTA.tblEncuestaBloquePlantilla (
	ENCUESTA_ID
	,TITULO_BLOQUE
	,ORDEN
	,ESTADO
	,FECHA_CREACION
	,USUARIO_CREACION
	)
	VALUES (
	@ENCUESTA_ID
	,@TITULO_BLOQUE
	,@ORDEN
	,@ESTADO
	,@FECHA_CREACION
	,@USUARIO_CREACION
	)

	SET @BLOQUE_ID = SCOPE_IDENTITY();
	SELECT @BLOQUE_ID;

END

GO

/*===========================================================
NOMBRE: ENCUESTA.sp_CrearPreguntaPlantilla
FECHA: 18/10/2025
AUTOR: ORESTES LA CUNZA - YACURUNA TECHNOLOGIES
OBJETIVO: CREACIÓN DE LAS PREGUNTAS QUE VAN ASOCIADAS A LOS BLOQUES DE LAS ENCUESTAS COMO PLANTILLAS
MODIFICACIONES:
NRO		FECHA		USUARIO		MODIFICACION
001		DD/MM/YYYY	
===========================================================*/
CREATE PROCEDURE ENCUESTA.sp_CrearPreguntaPlantilla
(
@BLOQUE_ID INT
,@TEXTO_PREGUNTA VARCHAR(200)
,@TIPO_PREGUNTA VARCHAR(MAX)
,@ORDEN INT
,@OPCIONES_JSON VARCHAR(MAX)
,@ESTADO BIT
,@FECHA_CREACION DATETIME
,@USUARIO_CREACION VARCHAR(50)
)
AS
BEGIN
SET NOCOUNT ON;

	INSERT INTO ENCUESTA.tblEncuestaPreguntaPlantilla (
	BLOQUE_ID
	,TEXTO_PREGUNTA
	,TIPO_PREGUNTA
	,ORDEN
	,OPCIONES_JSON
	,ESTADO
	,FECHA_CREACION
	,USUARIO_CREACION
	)
	VALUES (
	@BLOQUE_ID
	,@TEXTO_PREGUNTA
	,@TIPO_PREGUNTA
	,@ORDEN
	,@OPCIONES_JSON
	,@ESTADO
	,@FECHA_CREACION
	,@USUARIO_CREACION
	)

END

GO

/*===========================================================
NOMBRE: ENCUESTA.sp_ListarPlantillaEncuestas
FECHA: 18/10/2025
AUTOR: ORESTES LA CUNZA - YACURUNA TECHNOLOGIES
OBJETIVO: LISTAR TODAS LAS PLANTILLAS DE LAS ENCUESTAS CREADAS Y PAGINADAS
MODIFICACIONES:
NRO		FECHA		USUARIO		MODIFICACION
001		DD/MM/YYYY	
===========================================================*/
CREATE PROCEDURE ENCUESTA.sp_ListarPlantillaEncuestas
(
    @PageNumber INT = 1,
    @PageSize   INT = 10
)
AS
BEGIN
SET NOCOUNT ON;

	;WITH CTE AS
		(
			SELECT
				 T1.ENCUESTA_ID
				,T1.NOMBRE_ENCUESTA
				,T1.DESCRIPCION_ENCUESTA
				,T2.NOMBRE_ENCUESTA AS NOMBRE_TIPO_ENCUESTA
				,T1.FECHA_CREACION
			FROM ENCUESTA.tblEncuestaPlantilla T1
			INNER JOIN ENCUESTA.tblTipoEncuesta T2 ON T1.TIPO_ENCUESTA = T2.TIPO_ENCUESTA_ID
			WHERE T1.ESTADO = 1
		)
		SELECT *
		FROM CTE
		ORDER BY FECHA_CREACION DESC
		OFFSET (@PageNumber - 1) * @PageSize ROWS
		FETCH NEXT @PageSize ROWS ONLY;

END

GO

/*===========================================================
NOMBRE: ENCUESTA.sp_ListarAsignaturaEncuestas
FECHA: 18/10/2025
AUTOR: ORESTES LA CUNZA - YACURUNA TECHNOLOGIES
OBJETIVO: LISTAR TODAS LAS ENCUESTAS FINALES QUE FUERON CREADAS Y ENVIADAS POR CORREO A LOS ENCUESTADOS
MODIFICACIONES:
NRO		FECHA		USUARIO		MODIFICACION
001		DD/MM/YYYY	
===========================================================*/
CREATE PROCEDURE ENCUESTA.sp_ListarAsignaturaEncuestas
(
	@SECCION    NVARCHAR(50) = NULL,
    @MODULO     NVARCHAR(50) = NULL,
    @DOCENTE    NVARCHAR(100) = NULL,
    @FECHA_INICIO DATETIME = NULL,
    @FECHA_FIN   DATETIME = NULL,
    @PAGENUMBER INT = 1,
    @PAGESIZE   INT = 10
)
AS
BEGIN
SET NOCOUNT ON;

	;WITH CTE AS
		(
			SELECT
				T1.ENCUESTA_ID
				,T1.SEDE
				,T1.PERIODO
				,T1.TIPO_PROGRAMA
				,T1.SECCION
				,T1.MODULO
				,T1.TIPO_ENCUESTADO
				,T1.FECHA_ENVIO
			FROM ENCUESTA.tblEncuestaAsignatura T1
			WHERE 1 = 1
				AND (NULLIF(@SECCION, '') IS NULL OR T1.SECCION = @SECCION)
				AND (NULLIF(@MODULO, '') IS NULL OR T1.MODULO = @MODULO)
				AND (NULLIF(@DOCENTE, '') IS NULL OR T1.DOCENTE = @DOCENTE)
				AND (@FECHA_INICIO IS NULL OR @FECHA_INICIO = '1900-01-01' OR T1.FECHA_ENVIO >= @FECHA_INICIO)
				AND (@FECHA_FIN IS NULL OR @FECHA_FIN = '1900-01-01' OR T1.FECHA_ENVIO <= @FECHA_FIN)
		)
		SELECT *
		FROM CTE
		ORDER BY FECHA_ENVIO DESC
		OFFSET (@PageNumber - 1) * @PageSize ROWS
		FETCH NEXT @PageSize ROWS ONLY;

END

GO

/*===========================================================
NOMBRE: ENCUESTA.sp_ListarTipoEncuesta
FECHA: 18/10/2025
AUTOR: ORESTES LA CUNZA - YACURUNA TECHNOLOGIES
OBJETIVO: SIRVE PARA LISTAR EL TIPO DE ENCUESTA PARA UN COMBOBOX EN EL FRONT
MODIFICACIONES:
NRO		FECHA		USUARIO		MODIFICACION
001		DD/MM/YYYY	
===========================================================*/
CREATE PROCEDURE ENCUESTA.sp_ListarTipoEncuesta
AS
BEGIN
SET NOCOUNT ON;

	SELECT
		TIPO_ENCUESTA_ID
		,NOMBRE_ENCUESTA
	FROM ENCUESTA.tblTipoEncuesta
	WHERE ESTADO = 1

END

GO

/*===========================================================
NOMBRE: ENCUESTA.sp_ListarTipoEncuestado
FECHA: 18/10/2025
AUTOR: ORESTES LA CUNZA - YACURUNA TECHNOLOGIES
OBJETIVO: SIRVE PARA LISTAR EL TIPO DE ENCUESTADO PARA UN COMBO BOX EN LA VISTA
MODIFICACIONES:
NRO		FECHA		USUARIO		MODIFICACION
001		DD/MM/YYYY	
===========================================================*/
CREATE PROCEDURE ENCUESTA.sp_ListarTipoEncuestado
AS
BEGIN
SET NOCOUNT ON;

	SELECT
		TIPO_ENCUESTADO_ID
		,NOMBRE_ENCUESTADO
	FROM ENCUESTA.tblTipoEncuestado
	WHERE ESTADO = 1

END

GO

/*===========================================================
NOMBRE: ENCUESTA.sp_ListarPlantillaEncuestaId
FECHA: 18/10/2025
AUTOR: ORESTES LA CUNZA - YACURUNA TECHNOLOGIES
OBJETIVO: SIRVE PARA LISTAR EL DETALLE DE 1 PLANTILLA DE ENCUESTA
MODIFICACIONES:
NRO		FECHA		USUARIO		MODIFICACION
001		DD/MM/YYYY	
===========================================================*/
CREATE PROCEDURE ENCUESTA.sp_ListarPlantillaEncuestaId (@ENCUESTA_ID INT)
AS
BEGIN
SET NOCOUNT ON;

	SELECT
		 T1.ENCUESTA_ID
		,T1.NOMBRE_ENCUESTA
		,T1.DESCRIPCION_ENCUESTA
		,ISNULL(T2.TIPO_ENCUESTA_ID, 0) AS TIPO_ENCUESTA_ID
		,ISNULL(T2.NOMBRE_ENCUESTA, 'SIN TIPO ENCUESTA') AS TIPO_ENCUESTA
		,T1.FECHA_CREACION
	FROM ENCUESTA.tblEncuestaPlantilla T1
	LEFT JOIN ENCUESTA.tblTipoEncuesta T2 ON T2.TIPO_ENCUESTA_ID = T1.TIPO_ENCUESTA
	WHERE T1.ENCUESTA_ID = @ENCUESTA_ID
		AND T1.ESTADO = 1

	SELECT
		T2.BLOQUE_ID
		,T2.TITULO_BLOQUE
		,T2.ORDEN
	FROM ENCUESTA.tblEncuestaPlantilla T1
	INNER JOIN ENCUESTA.tblEncuestaBloquePlantilla T2 ON T1.ENCUESTA_ID = T2.ENCUESTA_ID
	WHERE T1.ENCUESTA_ID = @ENCUESTA_ID
		AND T2.ESTADO = 1

	SELECT
		T2.BLOQUE_ID
		,T3.PREGUNTA_ID
		,T3.TEXTO_PREGUNTA
		,T3.TIPO_PREGUNTA
		,T3.ORDEN
		,T3.OPCIONES_JSON
	FROM ENCUESTA.tblEncuestaPlantilla T1
	INNER JOIN ENCUESTA.tblEncuestaBloquePlantilla T2 ON T1.ENCUESTA_ID = T2.ENCUESTA_ID
	INNER JOIN ENCUESTA.tblEncuestaPreguntaPlantilla T3 ON T3.BLOQUE_ID = T2.BLOQUE_ID
	WHERE T1.ENCUESTA_ID = @ENCUESTA_ID
		AND T3.ESTADO = 1

END

GO

/*===========================================================
NOMBRE: ENCUESTA.sp_ListarAsignaturaEncuestaId
FECHA: 18/10/2025
AUTOR: ORESTES LA CUNZA - YACURUNA TECHNOLOGIES
OBJETIVO: SIRVE PARA LISTAR EL DETALLE DE LA ENCUESTA FINAL QUE YA FUE ENVIADA
MODIFICACIONES:
NRO		FECHA		USUARIO		MODIFICACION
001		DD/MM/YYYY	
===========================================================*/
CREATE PROCEDURE ENCUESTA.sp_ListarAsignaturaEncuestaId (@ENCUESTA_ID INT, @DNI_ENCUESTADO VARCHAR(20))
AS
BEGIN
SET NOCOUNT ON;

	DECLARE @EXISTE INT;

	SELECT @EXISTE = COUNT(1)
	FROM ENCUESTA.tblEncuestaAsignaturaAlumno
	WHERE 1 = 1
		AND ENCUESTA_ASIGNATURA_ID = @ENCUESTA_ID	--25
		AND DNI_ENCUESTADO = @DNI_ENCUESTADO	--'06602128'

	IF @EXISTE = 0
        SET @ENCUESTA_ID = -1;

	SELECT
		 T1.ENCUESTA_ID
		 ,T1.NOMBRE_ENCUESTA
		 ,T1.DESCRIPCION_ENCUESTA
		 ,T2.NOMBRE_ENCUESTA AS TIPO_ENCUESTA
		 ,T1.TIPO_PROGRAMA
		 ,T1.SEDE
		 ,T1.PERIODO
		 ,T1.SECCION
		 ,T1.MODULO
		 ,T1.DOCENTE
		 ,T1.FECHA_INICIO
		 ,T1.FECHA_FIN
	FROM ENCUESTA.tblEncuestaAsignatura T1
	INNER JOIN ENCUESTA.tblTipoEncuesta T2 ON T1.TIPO_ENCUESTA = T2.TIPO_ENCUESTA_ID
	WHERE T1.ENCUESTA_ID = @ENCUESTA_ID

	SELECT
		T2.BLOQUE_ID
		,T2.TITULO_BLOQUE
		,T2.ORDEN
	FROM ENCUESTA.tblEncuestaAsignatura T1
	INNER JOIN ENCUESTA.tblEncuestaBloqueAsignatura T2 ON T1.ENCUESTA_ID = T2.ENCUESTA_ID
	WHERE T1.ENCUESTA_ID = @ENCUESTA_ID

	SELECT
		T2.BLOQUE_ID
		,T3.PREGUNTA_ID
		,T3.TEXTO_PREGUNTA
		,T3.TIPO_PREGUNTA
		,T3.ORDEN
		,T3.OPCIONES_JSON
	FROM ENCUESTA.tblEncuestaAsignatura T1
	INNER JOIN ENCUESTA.tblEncuestaBloqueAsignatura T2 ON T1.ENCUESTA_ID = T2.ENCUESTA_ID
	INNER JOIN ENCUESTA.tblEncuestaPreguntaAsignatura T3 ON T3.BLOQUE_ID = T2.BLOQUE_ID
	WHERE T1.ENCUESTA_ID = @ENCUESTA_ID

END

GO

/*===========================================================
NOMBRE: ENCUESTA.sp_ListarEncuestasPendientes
FECHA: 18/10/2025
AUTOR: ORESTES LA CUNZA - YACURUNA TECHNOLOGIES
OBJETIVO: SIRVE PARA LISTAR LAS ENCUESTAS PENDIENTES POR USUARIO, QUE AÚN NO HAN SIDO COMPLETADAS
MODIFICACIONES:
NRO		FECHA		USUARIO		MODIFICACION
001		DD/MM/YYYY	
===========================================================*/
CREATE PROCEDURE ENCUESTA.sp_ListarEncuestasPendientes
(@ALUMNO_ID VARCHAR(50))
AS
BEGIN
SET NOCOUNT ON;

	SELECT
		T2.ENCUESTA_ID
		,T2.NOMBRE_ENCUESTA
		,T2.DESCRIPCION_ENCUESTA
		,T2.TIPO_ENCUESTA
		,T2.FECHA_INICIO
		,T2.FECHA_FIN
	FROM ENCUESTA.tblEncuestaAsignaturaAlumno T1
	INNER JOIN ENCUESTA.tblEncuestaAsignatura T2 ON T1.ENCUESTA_ASIGNATURA_ID = T2.ENCUESTA_ID
	WHERE T1.COMPLETADO = 0
		AND T2.ACTIVO = 1
		AND T1.DNI_ENCUESTADO = @ALUMNO_ID
	ORDER BY T2.FECHA_FIN ASC

END

GO

/*===========================================================
NOMBRE: ENCUESTA.sp_EditarPlantillaEncuesta
FECHA: 18/10/2025
AUTOR: ORESTES LA CUNZA - YACURUNA TECHNOLOGIES
OBJETIVO: SIRVE PARA EDITAR LA CABECERA DE LA PLANTILLA DE LA ENCUESTA
MODIFICACIONES:
NRO		FECHA		USUARIO		MODIFICACION
001		DD/MM/YYYY	
===========================================================*/
CREATE PROCEDURE ENCUESTA.sp_EditarPlantillaEncuesta
(
@ENCUESTA_ID INT
,@NOMBRE_ENCUESTA VARCHAR(200)
,@DESCRIPCION_ENCUESTA VARCHAR(200)
,@TIPO_ENCUESTA INT
,@USUARIO_MODIFICACION VARCHAR(50)
,@FECHA_MODIFICACION DATETIME
)
AS
BEGIN
SET NOCOUNT ON;

	UPDATE ENCUESTA.tblEncuestaPlantilla WITH (ROWLOCK)
	SET
		NOMBRE_ENCUESTA = @NOMBRE_ENCUESTA
		,DESCRIPCION_ENCUESTA = @DESCRIPCION_ENCUESTA
		,TIPO_ENCUESTA = @TIPO_ENCUESTA
		,USUARIO_MODIFICACION = @USUARIO_MODIFICACION
		,FECHA_MODIFICACION = @FECHA_MODIFICACION
	WHERE ENCUESTA_ID = @ENCUESTA_ID;

END


GO

/*===========================================================
NOMBRE: ENCUESTA.sp_EditarBloquePlantilla
FECHA: 18/10/2025
AUTOR: ORESTES LA CUNZA - YACURUNA TECHNOLOGIES
OBJETIVO: SIRVE PARA EDITAR EL BLOQUE DE LA PLANTILLA DE LA ENCUESTA
MODIFICACIONES:
NRO		FECHA		USUARIO		MODIFICACION
001		DD/MM/YYYY	
===========================================================*/
CREATE PROCEDURE ENCUESTA.sp_EditarBloquePlantilla
(
@BLOQUE_ID INT
,@TITULO_BLOQUE VARCHAR(200)
,@ORDEN INT
,@USUARIO_MODIFICACION VARCHAR(50)
,@FECHA_MODIFICACION DATETIME
)
AS
BEGIN
SET NOCOUNT ON;

	UPDATE ENCUESTA.tblEncuestaBloquePlantilla WITH (ROWLOCK)
	SET
		TITULO_BLOQUE = @TITULO_BLOQUE
		,ORDEN = @ORDEN
		,USUARIO_MODIFICACION = @USUARIO_MODIFICACION
		,FECHA_MODIFICACION = @FECHA_MODIFICACION
	WHERE BLOQUE_ID = @BLOQUE_ID

END


GO

/*===========================================================
NOMBRE: ENCUESTA.sp_EditarPreguntaPlantilla
FECHA: 18/10/2025
AUTOR: ORESTES LA CUNZA - YACURUNA TECHNOLOGIES
OBJETIVO: SIRVE PARA EDITAR LA PREGUNTA DE LA PLANTILLA DE LA ENCUESTA
MODIFICACIONES:
NRO		FECHA		USUARIO		MODIFICACION
001		DD/MM/YYYY	
===========================================================*/
CREATE PROCEDURE ENCUESTA.sp_EditarPreguntaPlantilla
(
@PREGUNTA_ID INT
,@TEXTO_PREGUNTA VARCHAR(200)
,@TIPO_PREGUNTA VARCHAR(MAX)
,@ORDEN INT
,@OPCIONES_JSON VARCHAR(MAX)
,@USUARIO_MODIFICACION VARCHAR(50)
,@FECHA_MODIFICACION DATETIME
)
AS
BEGIN
SET NOCOUNT ON;

	UPDATE ENCUESTA.tblEncuestaPreguntaPlantilla WITH (ROWLOCK)
	SET
		TEXTO_PREGUNTA = @TEXTO_PREGUNTA
		,TIPO_PREGUNTA = @TIPO_PREGUNTA
		,ORDEN = @ORDEN
		,OPCIONES_JSON = @OPCIONES_JSON
		,USUARIO_MODIFICACION = @USUARIO_MODIFICACION
		,FECHA_MODIFICACION = @FECHA_MODIFICACION
	WHERE PREGUNTA_ID = @PREGUNTA_ID;

END


GO

/*===========================================================
NOMBRE: ENCUESTA.sp_EliminarEncuestaPlantilla
FECHA: 18/10/2025
AUTOR: ORESTES LA CUNZA - YACURUNA TECHNOLOGIES
OBJETIVO: SIRVE PARA ELIMINAR LA ENCUESTA (CABECERA) DE LA PLANTILLA DE LA ENCUESTA, JUNTO A TODOS SUS BLOQUES Y PREGUNTAS ASOCIADAS
MODIFICACIONES:
NRO		FECHA		USUARIO		MODIFICACION
001		DD/MM/YYYY	
===========================================================*/
CREATE PROCEDURE ENCUESTA.sp_EliminarEncuestaPlantilla
(
@ENCUESTA_ID INT
,@USUARIO_MODIFICACION VARCHAR(50)
,@FECHA_MODIFICACION DATETIME
)
AS
BEGIN
SET NOCOUNT ON;

	UPDATE ENCUESTA.tblEncuestaPlantilla WITH (ROWLOCK)
	SET
		ESTADO = 0
		,USUARIO_MODIFICACION = @USUARIO_MODIFICACION
		,FECHA_MODIFICACION = @FECHA_MODIFICACION
	WHERE ENCUESTA_ID = @ENCUESTA_ID;

	UPDATE ENCUESTA.tblEncuestaBloquePlantilla WITH (ROWLOCK)
	SET
		ESTADO = 0
		,USUARIO_MODIFICACION = @USUARIO_MODIFICACION
		,FECHA_MODIFICACION = @FECHA_MODIFICACION
	WHERE ENCUESTA_ID = @ENCUESTA_ID;

	UPDATE T1 WITH (ROWLOCK)
	SET
		ESTADO = 0
		,USUARIO_MODIFICACION = @USUARIO_MODIFICACION
		,FECHA_MODIFICACION = @FECHA_MODIFICACION
	FROM ENCUESTA.tblEncuestaPreguntaPlantilla T1
	INNER JOIN ENCUESTA.tblEncuestaBloquePlantilla T2 ON T1.BLOQUE_ID = T2.BLOQUE_ID
	WHERE T2.ENCUESTA_ID = @ENCUESTA_ID;

END


GO

/*===========================================================
NOMBRE: ENCUESTA.sp_EliminarBloquePlantilla
FECHA: 18/10/2025
AUTOR: ORESTES LA CUNZA - YACURUNA TECHNOLOGIES
OBJETIVO: SIRVE PARA ELIMINAR TODO EL BLOQUE DE LA PLANTILLA DE LA ENCUESTA JUNTO A SUS PREGUNTAS ASOCIADAS
MODIFICACIONES:
NRO		FECHA		USUARIO		MODIFICACION
001		DD/MM/YYYY	
===========================================================*/
CREATE PROCEDURE ENCUESTA.sp_EliminarBloquePlantilla
(
@BLOQUE_ID INT
,@USUARIO_MODIFICACION VARCHAR(50)
,@FECHA_MODIFICACION DATETIME
)
AS
BEGIN
SET NOCOUNT ON;

	UPDATE ENCUESTA.tblEncuestaBloquePlantilla WITH (ROWLOCK)
	SET
		ESTADO = 0
		,USUARIO_MODIFICACION = @USUARIO_MODIFICACION
		,FECHA_MODIFICACION = @FECHA_MODIFICACION
	WHERE BLOQUE_ID = @BLOQUE_ID;

	UPDATE ENCUESTA.tblEncuestaPreguntaPlantilla WITH (ROWLOCK)
	SET
		ESTADO = 0
		,USUARIO_MODIFICACION = @USUARIO_MODIFICACION
		,FECHA_MODIFICACION = @FECHA_MODIFICACION
	WHERE BLOQUE_ID = @BLOQUE_ID;

END


GO

/*===========================================================
NOMBRE: ENCUESTA.sp_EliminarPreguntaPlantilla
FECHA: 18/10/2025
AUTOR: ORESTES LA CUNZA - YACURUNA TECHNOLOGIES
OBJETIVO: SIRVE PARA ELIMINAR UNA PREGUNTA DE LA PLANTILLA DE LA ENCUESTA
MODIFICACIONES:
NRO		FECHA		USUARIO		MODIFICACION
001		DD/MM/YYYY	
===========================================================*/
CREATE PROCEDURE ENCUESTA.sp_EliminarPreguntaPlantilla
(
@PREGUNTA_ID INT
,@USUARIO_MODIFICACION VARCHAR(50)
,@FECHA_MODIFICACION DATETIME
)
AS
BEGIN
SET NOCOUNT ON;

	UPDATE ENCUESTA.tblEncuestaPreguntaPlantilla WITH (ROWLOCK)
	SET
		ESTADO = 0
		,USUARIO_MODIFICACION = @USUARIO_MODIFICACION
		,FECHA_MODIFICACION = @FECHA_MODIFICACION
	WHERE PREGUNTA_ID = @PREGUNTA_ID;

END

GO

/*===========================================================
NOMBRE: ENCUESTA.sp_CrearRespuestaEncuesta
FECHA: 18/10/2025
AUTOR: ORESTES LA CUNZA - YACURUNA TECHNOLOGIES
OBJETIVO: SIRVE PARA CREAR UNA RESPUESTA SOBRE EL ENCABEZADO DE LA ENCUESTA ENVIADA POR CORREO POR USUARIO
MODIFICACIONES:
NRO		FECHA		USUARIO		MODIFICACION
001		DD/MM/YYYY	
===========================================================*/
CREATE PROCEDURE ENCUESTA.sp_CrearRespuestaEncuesta
(
@ENCUESTA_ID INT
,@ALUMNO_ID VARCHAR(50)
,@FECHA_RESPUESTA DATETIME
,@COMPLETADO BIT
)
AS
BEGIN
SET NOCOUNT ON;

	DECLARE @RESPUESTA_ENCUESTA_ID INT;

	INSERT INTO ENCUESTA.tblRespuetaEncuesta
	(
		ENCUESTA_ID
		,ALUMNO_ID
		,FECHA_RESPUESTA
		,COMPLETADO
	)
	VALUES
	(
		@ENCUESTA_ID
		,@ALUMNO_ID
		,@FECHA_RESPUESTA
		,@COMPLETADO
	);

	SET @RESPUESTA_ENCUESTA_ID = SCOPE_IDENTITY();
	
	SELECT @RESPUESTA_ENCUESTA_ID AS RESPUESTA_ENCUESTA_ID;

END

GO

/*===========================================================
NOMBRE: ENCUESTA.sp_CrearRespuestaPregunta
FECHA: 18/10/2025
AUTOR: ORESTES LA CUNZA - YACURUNA TECHNOLOGIES
OBJETIVO: SIRVE PARA CREAR LAS RESPUESTAS REGISTRADAS POR ENCUESTA ENVIADA POR CORREO POR USUARIO
MODIFICACIONES:
NRO		FECHA		USUARIO		MODIFICACION
001		DD/MM/YYYY	
===========================================================*/
CREATE PROCEDURE ENCUESTA.sp_CrearRespuestaPregunta
(
@RESPUESTA_ENCUESTA_ID INT
,@ENCUESTA_PREGUNTA_ID INT
,@VALOR_RESPUESTA NVARCHAR(MAX)
)
AS
BEGIN
SET NOCOUNT ON;

	INSERT INTO ENCUESTA.tblRespuetaPregunta (
		RESPUESTA_ENCUESTA_ID
		,ENCUESTA_PREGUNTA_ID
		,VALOR_RESPUESTA
	)
	VALUES (
		@RESPUESTA_ENCUESTA_ID
		,@ENCUESTA_PREGUNTA_ID
		,@VALOR_RESPUESTA
	);

END

GO

/*===========================================================
NOMBRE: ENCUESTA.sp_ValidarRespuestaAlumno
FECHA: 18/10/2025
AUTOR: ORESTES LA CUNZA - YACURUNA TECHNOLOGIES
OBJETIVO: SIRVE PARA VALIDAR SI EL ALUMNO YA HA RESPONDIDO LA ENCUESTA O NO, PARA EVITAR RESPUESTAS DUPLICADAS
MODIFICACIONES:
NRO		FECHA		USUARIO		MODIFICACION
001		DD/MM/YYYY	
===========================================================*/
CREATE PROCEDURE ENCUESTA.sp_ValidarRespuestaAlumno
(
@ENCUESTA_ID INT
,@ALUMNO_ID VARCHAR(50)
)
AS
BEGIN
SET NOCOUNT ON;

	SELECT 1
	FROM ENCUESTA.tblRespuetaEncuesta
	WHERE ENCUESTA_ID = @ENCUESTA_ID
		AND ALUMNO_ID = @ALUMNO_ID
		AND COMPLETADO = 1

END

GO

/*===========================================================
NOMBRE: ENCUESTA.sp_ValidarEncuestaActiva
FECHA: 18/10/2025
AUTOR: ORESTES LA CUNZA - YACURUNA TECHNOLOGIES
OBJETIVO: SIRVE PARA VALIDAR SI LA ENCUESTA ENVIADA POR CORREO CONTINUA ACTIVA PARA QUE PUEDA SER RESPONDIDA O NO.
MODIFICACIONES:
NRO		FECHA		USUARIO		MODIFICACION
001		DD/MM/YYYY	
===========================================================*/
CREATE PROCEDURE ENCUESTA.sp_ValidarEncuestaActiva
@ENCUESTA_ID INT
AS
BEGIN
SET NOCOUNT ON;

	SELECT 1 FROM ENCUESTA.tblEncuestaAsignatura
	WHERE ENCUESTA_ID = @ENCUESTA_ID
		AND ACTIVO = 1
		AND GETDATE() BETWEEN FECHA_INICIO AND FECHA_FIN

END

GO

/*===========================================================
NOMBRE: ENCUESTA.sp_ListarRencuestasRespondidas
FECHA: 18/10/2025
AUTOR: ORESTES LA CUNZA - YACURUNA TECHNOLOGIES
OBJETIVO: SIRVE PARA LISTAR LAS ENCUESTAS QUE HAN SIDO RESPONDIDAS POR ALUMNO
MODIFICACIONES:
NRO		FECHA		USUARIO		MODIFICACION
001		DD/MM/YYYY	
===========================================================*/
CREATE PROCEDURE ENCUESTA.sp_ListarRencuestasRespondidas
    @ALUMNO_ID VARCHAR(50)
AS
BEGIN
SET NOCOUNT ON;

    SELECT RE.ALUMNO_ID,
        RE.ENCUESTA_ID,
        E.NOMBRE_ENCUESTA,
        RE.FECHA_RESPUESTA,
        RE.COMPLETADO
    FROM ENCUESTA.tblRespuetaEncuesta RE
    INNER JOIN ENCUESTA.tblEncuestaPlantilla E ON E.ENCUESTA_ID = RE.ENCUESTA_ID
    WHERE RE.ALUMNO_ID = @ALUMNO_ID
    ORDER BY RE.FECHA_RESPUESTA DESC;
END

GO

/*===========================================================
NOMBRE: ENCUESTA.sp_ActualizarEncuestaEnviada
FECHA: 18/10/2025
AUTOR: ORESTES LA CUNZA - YACURUNA TECHNOLOGIES
OBJETIVO: SIRVE PARA ACTUALIZAR EL FLAG A COMPLETADO DE LAS ENCUESTAS RESPONDIDAS POR ALUMNO PARA QUE DEJEN DE ESTAR PENDIENTE EN SU BANDEJA
MODIFICACIONES:
NRO		FECHA		USUARIO		MODIFICACION
001		DD/MM/YYYY	
===========================================================*/
CREATE PROCEDURE ENCUESTA.sp_ActualizarEncuestaEnviada
(
@ENCUESTA_ASIGNATURA_ID INT
,@ALUMNO_ID VARCHAR(50)
)
AS
BEGIN
SET NOCOUNT ON;

	UPDATE ENCUESTA.tblEncuestaAsignaturaAlumno
	SET COMPLETADO = 1
		,FECHA_RESPUESTA = GETDATE()
	WHERE ENCUESTA_ASIGNATURA_ID = @ENCUESTA_ASIGNATURA_ID
		AND DNI_ENCUESTADO = @ALUMNO_ID

END

GO

/*===========================================================
NOMBRE: ENCUESTA.sp_ListarReporteEncuestaDocente
FECHA: 18/10/2025
AUTOR: ORESTES LA CUNZA - YACURUNA TECHNOLOGIES
OBJETIVO: SIRVE PARA LISTAR EL REPORTE DE DATOS POR DOCENTE QUE SE MUESTRA EN EL DASHBOARD DE LA VISTA FRONTEND Y PARA EXPORTAR EN EXCEL Y PDF
MODIFICACIONES:
NRO		FECHA		USUARIO		MODIFICACION
001		DD/MM/YYYY	
===========================================================*/
CREATE PROCEDURE ENCUESTA.sp_ListarReporteEncuestaDocente
(@ENCUESTA_ID INT)
AS
BEGIN
SET NOCOUNT ON;

	SELECT
		ROW_NUMBER() OVER (ORDER BY T3.TEXTO_PREGUNTA) AS NRO,
		T4.TITULO_BLOQUE,
		T3.TEXTO_PREGUNTA,
		ROUND(AVG(CAST(JSON_VALUE(T2.VALOR_RESPUESTA, '$.score') AS FLOAT)), 2) AS PROMEDIO_SCORE
	FROM ENCUESTA.tblRespuetaEncuesta T1
	INNER JOIN ENCUESTA.tblRespuetaPregunta T2 ON T1.RESPUESTA_ENCUESTA_ID = T2.RESPUESTA_ENCUESTA_ID
	INNER JOIN ENCUESTA.tblEncuestaPreguntaAsignatura T3 ON T3.PREGUNTA_ID = T2.ENCUESTA_PREGUNTA_ID
	INNER JOIN ENCUESTA.tblEncuestaBloqueAsignatura T4 ON T4.BLOQUE_ID = T3.BLOQUE_ID
	WHERE 1 = 1
		AND T1.ENCUESTA_ID = @ENCUESTA_ID
		AND ISJSON(T2.VALOR_RESPUESTA) = 1
		AND JSON_VALUE(T2.VALOR_RESPUESTA, '$.score') IS NOT NULL
	GROUP BY 
		T4.TITULO_BLOQUE,
		T3.TEXTO_PREGUNTA,
		T2.ENCUESTA_PREGUNTA_ID
	ORDER BY NRO

    SELECT
		ROUND(AVG(CAST(JSON_VALUE(T2.VALOR_RESPUESTA, '$.score') AS FLOAT)), 2) AS PROMEDIO_TOTAL_SCORE
	FROM ENCUESTA.tblRespuetaEncuesta T1
	INNER JOIN ENCUESTA.tblRespuetaPregunta T2 ON T1.RESPUESTA_ENCUESTA_ID = T2.RESPUESTA_ENCUESTA_ID
	INNER JOIN ENCUESTA.tblEncuestaPreguntaAsignatura T3 ON T3.PREGUNTA_ID = T2.ENCUESTA_PREGUNTA_ID
	INNER JOIN ENCUESTA.tblEncuestaBloqueAsignatura T4 ON T4.BLOQUE_ID = T3.BLOQUE_ID
	WHERE 1 = 1
		AND T1.ENCUESTA_ID = @ENCUESTA_ID
		AND ISJSON(T2.VALOR_RESPUESTA) = 1
		AND JSON_VALUE(T2.VALOR_RESPUESTA, '$.score') IS NOT NULL

	SELECT
		NOMBRE_ENCUESTA
		,DOCENTE
		,PERIODO
		,SECCION
		,FECHA_INICIO
		,FECHA_FIN
	FROM ENCUESTA.tblEncuestaAsignatura
	WHERE ENCUESTA_ID = @ENCUESTA_ID

END

GO

/*===========================================================
NOMBRE: ENCUESTA.sp_ListarReporteEncuestaAlumno
FECHA: 18/10/2025
AUTOR: ORESTES LA CUNZA - YACURUNA TECHNOLOGIES
OBJETIVO: SIRVE PARA LISTAR EL REPORTE DE DATOS POR ALUMNO QUE SE MUESTRA EN EL DASHBOARD DE LA VISTA FRONTEND Y PARA EXPORTAR EN EXCEL Y PDF
MODIFICACIONES:
NRO		FECHA		USUARIO		MODIFICACION
001		DD/MM/YYYY	
===========================================================*/
CREATE PROCEDURE ENCUESTA.sp_ListarReporteEncuestaAlumno
(@ENCUESTA_ID INT)
AS
BEGIN
SET NOCOUNT ON;

	SELECT
		NOMBRE_ENCUESTA
		,DOCENTE
		,PERIODO
		,FECHA_INICIO
		,FECHA_FIN
		,TIPO_PROGRAMA AS PROGRAMA
		,SECCION
		,MODULO AS ASIGNATURA
	FROM ENCUESTA.tblEncuestaAsignatura
	WHERE ENCUESTA_ID = @ENCUESTA_ID

	;WITH CTE_PREGUNTAS AS (
    
			SELECT 
				T2.BLOQUE_ID
				,T2.TITULO_BLOQUE
				,T3.TEXTO_PREGUNTA
				,CAST(ROUND(AVG(CAST(JSON_VALUE(T5.VALOR_RESPUESTA, '$.score') AS FLOAT)), 2) AS DECIMAL(5,2)) AS PROMEDIO_SCORE
				,CAST(ROUND(100.0 * SUM(CASE WHEN JSON_VALUE(T5.VALOR_RESPUESTA, '$.score') = '1' THEN 1 ELSE 0 END) / COUNT(1), 2) AS DECIMAL(5,2)) AS PCT_1
				,CAST(ROUND(100.0 * SUM(CASE WHEN JSON_VALUE(T5.VALOR_RESPUESTA, '$.score') = '2' THEN 1 ELSE 0 END) / COUNT(1), 2) AS DECIMAL(5,2)) AS PCT_2
				,CAST(ROUND(100.0 * SUM(CASE WHEN JSON_VALUE(T5.VALOR_RESPUESTA, '$.score') = '3' THEN 1 ELSE 0 END) / COUNT(1), 2) AS DECIMAL(5,2)) AS PCT_3
				,CAST(ROUND(100.0 * SUM(CASE WHEN JSON_VALUE(T5.VALOR_RESPUESTA, '$.score') = '4' THEN 1 ELSE 0 END) / COUNT(1), 2) AS DECIMAL(5,2)) AS PCT_4
				,CAST(ROUND(100.0 * SUM(CASE WHEN JSON_VALUE(T5.VALOR_RESPUESTA, '$.score') = '5' THEN 1 ELSE 0 END) / COUNT(1), 2) AS DECIMAL(5,2)) AS PCT_5
			FROM ENCUESTA.tblEncuestaAsignatura T1
			INNER JOIN ENCUESTA.tblEncuestaBloqueAsignatura T2 ON T1.ENCUESTA_ID = T2.ENCUESTA_ID
					AND T1.ENCUESTA_ID = @ENCUESTA_ID
					AND T1.TIPO_ENCUESTADO = 1
			INNER JOIN ENCUESTA.tblEncuestaPreguntaAsignatura T3 ON T3.BLOQUE_ID = T2.BLOQUE_ID
			INNER JOIN ENCUESTA.tblRespuetaEncuesta T4 ON T4.ENCUESTA_ID = T1.ENCUESTA_ID
			INNER JOIN ENCUESTA.tblRespuetaPregunta T5 ON T5.RESPUESTA_ENCUESTA_ID = T4.RESPUESTA_ENCUESTA_ID
				AND T5.ENCUESTA_PREGUNTA_ID = T3.PREGUNTA_ID
			WHERE	1 = 1
					AND ISJSON(T5.VALOR_RESPUESTA) = 1
					AND JSON_VALUE(T5.VALOR_RESPUESTA, '$.score') IS NOT NULL
			GROUP BY T2.BLOQUE_ID
				,T2.TITULO_BLOQUE
				,T3.TEXTO_PREGUNTA
				,T5.ENCUESTA_PREGUNTA_ID
	)
	SELECT 
		BLOQUE_ID,
		TITULO_BLOQUE,
		TEXTO_PREGUNTA,
		PROMEDIO_SCORE,
		PCT_1, PCT_2, PCT_3, PCT_4, PCT_5
	FROM CTE_PREGUNTAS

	UNION ALL

	-- Promedio general por bloque
	SELECT 
		BLOQUE_ID,
		TITULO_BLOQUE,
		'PROMEDIO DEL BLOQUE' AS TEXTO_PREGUNTA,
		CAST(ROUND(AVG(PROMEDIO_SCORE), 2) AS DECIMAL(5,2)) AS PROMEDIO_SCORE,
		0, 0, 0, 0, 0
	FROM CTE_PREGUNTAS
	GROUP BY BLOQUE_ID, TITULO_BLOQUE
	ORDER BY BLOQUE_ID

	SELECT
		COUNT(ALUMNO_ID) AS CANTIDAD_MATRICULADOS,
		SUM(CASE WHEN COMPLETADO = 1 THEN 1 ELSE 0 END) AS CANTIDAD_ENCUESTADOS,
		CAST(
			100.0 * 
			SUM(CASE WHEN COMPLETADO = 1 THEN 1 ELSE 0 END) / 
			NULLIF(COUNT(ALUMNO_ID), 0)
		AS DECIMAL(5,2)) AS PORCENTAJE_ENCUESTADOS
	FROM ENCUESTA.tblRespuetaEncuesta
	WHERE ENCUESTA_ID = @ENCUESTA_ID

END

GO

/*===========================================================
NOMBRE: ENCUESTA.sp_ListarReporteEncuestaAsesor
FECHA: 18/10/2025
AUTOR: ORESTES LA CUNZA - YACURUNA TECHNOLOGIES
OBJETIVO: SIRVE PARA LISTAR EL REPORTE DE DATOS POR ASESOR ADMINISTRATIVO QUE SE MUESTRA EN EL DASHBOARD DE LA VISTA FRONTEND Y PARA EXPORTAR EN EXCEL Y PDF
MODIFICACIONES:
NRO		FECHA		USUARIO		MODIFICACION
001		DD/MM/YYYY	
===========================================================*/
CREATE PROCEDURE ENCUESTA.sp_ListarReporteEncuestaAsesor
(@ENCUESTA_ID INT)
AS
BEGIN
SET NOCOUNT ON;

	SELECT
		NOMBRE_ENCUESTA
		,DOCENTE
		,PERIODO
		,FECHA_INICIO
		,FECHA_FIN
		,TIPO_PROGRAMA AS PROGRAMA
		,SECCION
		,MODULO AS ASIGNATURA
	FROM ENCUESTA.tblEncuestaAsignatura
	WHERE ENCUESTA_ID = @ENCUESTA_ID

	SELECT
		ROW_NUMBER() OVER (ORDER BY T2.BLOQUE_ID, T5.ENCUESTA_PREGUNTA_ID) AS NRO
		,T2.TITULO_BLOQUE
		,T3.TEXTO_PREGUNTA
		,AVG(CAST(JSON_VALUE(T5.VALOR_RESPUESTA, '$.score') AS FLOAT)) AS PROMEDIO_SCORE
	FROM ENCUESTA.tblEncuestaAsignatura T1
	INNER JOIN ENCUESTA.tblEncuestaBloqueAsignatura T2 ON T1.ENCUESTA_ID = T2.ENCUESTA_ID
			AND T1.ENCUESTA_ID = @ENCUESTA_ID
			AND T1.TIPO_ENCUESTADO = 3
	INNER JOIN ENCUESTA.tblEncuestaPreguntaAsignatura T3 ON T3.BLOQUE_ID = T2.BLOQUE_ID
	INNER JOIN ENCUESTA.tblRespuetaEncuesta T4 ON T4.ENCUESTA_ID = T1.ENCUESTA_ID
	INNER JOIN ENCUESTA.tblRespuetaPregunta T5 ON T5.RESPUESTA_ENCUESTA_ID = T4.RESPUESTA_ENCUESTA_ID
		AND T5.ENCUESTA_PREGUNTA_ID = T3.PREGUNTA_ID
	WHERE	1 = 1
			AND ISJSON(T5.VALOR_RESPUESTA) = 1
			AND JSON_VALUE(T5.VALOR_RESPUESTA, '$.score') IS NOT NULL
	GROUP BY T2.BLOQUE_ID
		,T2.TITULO_BLOQUE
		,T3.TEXTO_PREGUNTA
		,T5.ENCUESTA_PREGUNTA_ID

	SELECT
		ROUND(AVG(CAST(JSON_VALUE(T5.VALOR_RESPUESTA, '$.score') AS FLOAT)), 2) AS PROMEDIO_TOTAL_SCORE
	FROM ENCUESTA.tblEncuestaAsignatura T1
	INNER JOIN ENCUESTA.tblEncuestaBloqueAsignatura T2 ON T1.ENCUESTA_ID = T2.ENCUESTA_ID
			AND T1.ENCUESTA_ID = @ENCUESTA_ID
			AND T1.TIPO_ENCUESTADO = 3
	INNER JOIN ENCUESTA.tblEncuestaPreguntaAsignatura T3 ON T3.BLOQUE_ID = T2.BLOQUE_ID
	INNER JOIN ENCUESTA.tblRespuetaEncuesta T4 ON T4.ENCUESTA_ID = T1.ENCUESTA_ID
	INNER JOIN ENCUESTA.tblRespuetaPregunta T5 ON T5.RESPUESTA_ENCUESTA_ID = T4.RESPUESTA_ENCUESTA_ID
		AND T5.ENCUESTA_PREGUNTA_ID = T3.PREGUNTA_ID
	WHERE	1 = 1
			AND ISJSON(T5.VALOR_RESPUESTA) = 1
			AND JSON_VALUE(T5.VALOR_RESPUESTA, '$.score') IS NOT NULL

END


GO


/*===========================================================
NOMBRE: ENCUESTA.sp_ListarReporteParaExcel
FECHA: 18/10/2025
AUTOR: ORESTES LA CUNZA - YACURUNA TECHNOLOGIES
OBJETIVO: SIRVE PARA LISTAR EL REPORTE DE DATOS POR GENERAL QUE SE EXPORTARÁ EN EXCEL
MODIFICACIONES:
NRO		FECHA		USUARIO		MODIFICACION
001		DD/MM/YYYY	
===========================================================*/
CREATE PROCEDURE ENCUESTA.sp_ListarReporteParaExcel (@ENCUESTA_ID INT)
AS
BEGIN
SET NOCOUNT ON;

--==========================INICIO HOJA1 DE EXCEL============================================
	SELECT
		 T1.TIPO_PROGRAMA AS PROGRAMA
		,T1.MODULO AS ASIGNATURA
		,T1.DOCENTE		--CRUZAR CON NOMBRE DOCENTE
		,'Del ' + CONVERT(VARCHAR(20), T1.FECHA_INICIO, 103) + ' '
		+ 'Hasta el ' + CONVERT(VARCHAR(20), T1.FECHA_FIN, 103) AS FECHAS
		 ,T2.NOMBRE_ENCUESTA AS TIPO_ENCUESTA
	FROM ENCUESTA.tblEncuestaAsignatura T1
	INNER JOIN ENCUESTA.tblTipoEncuesta T2 ON T1.TIPO_ENCUESTA = T2.TIPO_ENCUESTA_ID
	WHERE T1.ENCUESTA_ID = @ENCUESTA_ID

	SELECT
		T2.BLOQUE_ID
		,T2.TITULO_BLOQUE
		,T2.ORDEN
		--PROMEDIO DE PUNTAJE DEL BLOQUE
		,ROUND(AVG(CAST(JSON_VALUE(T4.VALOR_RESPUESTA, '$.score') AS FLOAT)), 2) AS PROMEDIO_BLOQUE
	FROM ENCUESTA.tblEncuestaAsignatura T1
	INNER JOIN ENCUESTA.tblEncuestaBloqueAsignatura T2 ON T1.ENCUESTA_ID = T2.ENCUESTA_ID
	INNER JOIN ENCUESTA.tblEncuestaPreguntaAsignatura T3 ON T3.BLOQUE_ID = T2.BLOQUE_ID
	INNER JOIN ENCUESTA.tblRespuetaPregunta T4 ON T4.ENCUESTA_PREGUNTA_ID = T3.PREGUNTA_ID
	WHERE T1.ENCUESTA_ID = @ENCUESTA_ID
		AND ISJSON(T4.VALOR_RESPUESTA) = 1
		AND JSON_VALUE(T4.VALOR_RESPUESTA, '$.score') IS NOT NULL
	GROUP BY 
		T2.BLOQUE_ID
		,T2.TITULO_BLOQUE
		,T2.ORDEN

	SELECT
		T2.BLOQUE_ID
		,T3.PREGUNTA_ID
		,T3.TEXTO_PREGUNTA
		,T3.TIPO_PREGUNTA
		,T3.ORDEN
		,T3.OPCIONES_JSON
		,ROUND(AVG(CAST(JSON_VALUE(T4.VALOR_RESPUESTA, '$.score') AS FLOAT)), 2) AS PROMEDIO_PREGUNTA
	FROM ENCUESTA.tblEncuestaAsignatura T1
	INNER JOIN ENCUESTA.tblEncuestaBloqueAsignatura T2 ON T1.ENCUESTA_ID = T2.ENCUESTA_ID
	INNER JOIN ENCUESTA.tblEncuestaPreguntaAsignatura T3 ON T3.BLOQUE_ID = T2.BLOQUE_ID
	INNER JOIN ENCUESTA.tblRespuetaPregunta T4 ON T4.ENCUESTA_PREGUNTA_ID = T3.PREGUNTA_ID
	WHERE T1.ENCUESTA_ID = @ENCUESTA_ID
		AND ISJSON(T4.VALOR_RESPUESTA) = 1
		AND JSON_VALUE(T4.VALOR_RESPUESTA, '$.score') IS NOT NULL
	GROUP BY T2.BLOQUE_ID
			,T3.PREGUNTA_ID
			,T3.TEXTO_PREGUNTA
			,T3.TIPO_PREGUNTA
			,T3.ORDEN
			,T3.OPCIONES_JSON

	SELECT 
		ROUND(AVG(CAST(JSON_VALUE(T4.VALOR_RESPUESTA, '$.score') AS FLOAT)), 2) AS PROMEDIO_TOTAL
	FROM ENCUESTA.tblEncuestaAsignatura T1
	INNER JOIN ENCUESTA.tblEncuestaBloqueAsignatura T2 ON T1.ENCUESTA_ID = T2.ENCUESTA_ID
	INNER JOIN ENCUESTA.tblEncuestaPreguntaAsignatura T3 ON T3.BLOQUE_ID = T2.BLOQUE_ID
	INNER JOIN ENCUESTA.tblRespuetaPregunta T4 ON T4.ENCUESTA_PREGUNTA_ID = T3.PREGUNTA_ID
	WHERE T1.ENCUESTA_ID = @ENCUESTA_ID
		AND ISJSON(T4.VALOR_RESPUESTA) = 1
		AND JSON_VALUE(T4.VALOR_RESPUESTA, '$.score') IS NOT NULL

--==========================FIN HOJA1 DE EXCEL============================================

--==========================INICIO RESPUESTAS DE EXCEL============================================

--CABECERA
SELECT
	T1.ENCUESTA_ID
	--T4.RESPUESTA_ID  -- asume que existe, o puedes usar ID del alumno
	,T3.PREGUNTA_ID
	,T3.TEXTO_PREGUNTA
	,ISNULL(JSON_VALUE(T4.VALOR_RESPUESTA, '$.comentario'), '') AS COMENTARIO
FROM ENCUESTA.tblEncuestaAsignatura T1
INNER JOIN ENCUESTA.tblEncuestaBloqueAsignatura T2 ON T1.ENCUESTA_ID = T2.ENCUESTA_ID
INNER JOIN ENCUESTA.tblEncuestaPreguntaAsignatura T3 ON T3.BLOQUE_ID = T2.BLOQUE_ID
INNER JOIN ENCUESTA.tblRespuetaPregunta T4 ON T4.ENCUESTA_PREGUNTA_ID = T3.PREGUNTA_ID
WHERE T1.ENCUESTA_ID = @ENCUESTA_ID
	AND ISJSON(T4.VALOR_RESPUESTA) = 1

--CONTENIDO PIVOT
DECLARE @cols NVARCHAR(MAX);
DECLARE @query NVARCHAR(MAX);

SELECT @cols = STRING_AGG(QUOTENAME(CAST(ENCUESTA_PREGUNTA_ID AS NVARCHAR(10))), ',')
FROM (
	SELECT DISTINCT ENCUESTA_PREGUNTA_ID
	FROM ENCUESTA.tblRespuetaPregunta T1
	INNER JOIN ENCUESTA.tblRespuetaEncuesta T2 
		ON T1.RESPUESTA_ENCUESTA_ID = T2.RESPUESTA_ENCUESTA_ID
	WHERE T2.ENCUESTA_ID = @ENCUESTA_ID
) X;

SET @query = '
SELECT 
	p.ENCUESTA_ID,
	p.ALUMNO_ID,
	' + @cols + ',
	'''' AS COMENTARIO
FROM (
	SELECT 
		T1.ENCUESTA_ID,
		T1.ALUMNO_ID,
		T2.ENCUESTA_PREGUNTA_ID,
		CAST(JSON_VALUE(T2.VALOR_RESPUESTA, ''$.score'') AS FLOAT) AS SCORE
	FROM ENCUESTA.tblRespuetaEncuesta T1
	INNER JOIN ENCUESTA.tblRespuetaPregunta T2 
		ON T1.RESPUESTA_ENCUESTA_ID = T2.RESPUESTA_ENCUESTA_ID
	WHERE T1.ENCUESTA_ID = @pEncuestaId
		AND ISJSON(T2.VALOR_RESPUESTA) = 1
) src
PIVOT (
	MAX(SCORE)
	FOR ENCUESTA_PREGUNTA_ID IN (' + @cols + ')
) p
';

EXEC sp_executesql 
	@query, 
	N'@pEncuestaId INT', 
	@pEncuestaId = @ENCUESTA_ID;
SELECT PREGUNTA_ID, TEXTO_PREGUNTA
FROM ENCUESTA.tblEncuestaPreguntaAsignatura
WHERE BLOQUE_ID IN (
    SELECT BLOQUE_ID FROM ENCUESTA.tblEncuestaBloqueAsignatura WHERE ENCUESTA_ID = @ENCUESTA_ID
)
ORDER BY ORDEN;

--==========================FIN RESPUESTAS DE EXCEL============================================

--==========================INICIO RESUMEN DE EXCEL============================================

SELECT COUNT(1) AS CANTIDAD_ENCUESTADOS
FROM ENCUESTA.tblRespuetaEncuesta
WHERE ENCUESTA_ID = @ENCUESTA_ID
	AND COMPLETADO = 1

--==========================FIN RESUMEN DE EXCEL============================================

END

